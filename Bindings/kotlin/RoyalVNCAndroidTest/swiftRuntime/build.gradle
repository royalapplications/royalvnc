plugins {
    alias(libs.plugins.android.library)
    id('maven-publish')
}

group = 'com.royalapps'
def mavenArtifactId = 'swiftruntime_android'
version = System.getenv('SWIFT_RT_VERSION') ?: '6.2.1'

android {
    namespace "${group}.${mavenArtifactId}"
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        minSdk = 29
    }

    buildTypes {
        release {
            minifyEnabled = false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    publishing {
        singleVariant('release') {}
    }
}

dependencies {
    implementation libs.androidx.core.ktx
}

// https://developer.android.com/build/publish-library/upload-library#create-pub
publishing {
    publications {
        release(MavenPublication) {
            artifactId = mavenArtifactId

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            name = 'local'
            url = layout.buildDirectory.dir('local')
        }
    }
}

// https://developer.android.com/build/publish-library/upload-library#local-repo
tasks.register('publishMavenBundle', Zip) {
    def publishTask = tasks.named('publishReleasePublicationToLocalRepository')
    from publishTask.map { it.getRepository().getUrl() }
    archiveFileName.set("${mavenArtifactId}.zip")
}
